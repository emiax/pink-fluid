global string $win;
global string $bubbleList;

global proc loadState(string $filename[]) {
  pfLoadState($filename);
}

global proc openFileBrowser() {
  string $result[] = `fileDialog2 -fileMode 4 -fileFilter "Pink Fluid (*.pf)"`;
  loadState($result);
}

global proc addBubbles() {
  global string $bubbleList;

  string $selectionShapes[] = `ls -sl -dag -lf`;
  string $selectionNames[] = `ls -sl`;
  if (size($selectionShapes) == 1 && `nodeType $selectionShapes[0]` == "particle") {
    string $particlesName = $selectionNames[0];
    string $frame = `currentTime -q`;
    pfSetBubbles($particlesName, (int)$frame);

    $listEntry = $particlesName + "\t" + $frame;
    $list = `textScrollList -edit -append $listEntry $bubbleList`;

    print($particlesName + " added as bubbles in frame " + $frame + "\n");
  } else {
    print("A particle system must be selected\n");
  }
}

global proc removeBubbles() {
  global string $bubbleList;
  string $selectedItems[] = `textScrollList -query -selectItem $bubbleList`;
  int $selectedItemIndices[] = `textScrollList -query -selectIndexedItem $bubbleList`;

  int $nItems = size($selectedItems);
  if ($nItems > 0) {
    for ($i = $nItems - 1; $i > -1; --$i) {
      string $item = $selectedItems[$i];
      int $itemIndex = $selectedItemIndices[$i];
      string $splitEntry[] = stringToStringArray($item, "\t");
      int $frame = $splitEntry[1];
      pfRemoveBubbles($frame);
      $status = `textScrollList -edit -removeIndexedItem $itemIndex $bubbleList`;
    }
  } else {
    print("A list item must be selected\n");
  }
}

global proc appendBubbleList() {
  global string $bubbleList;
  $bubbleList = `textScrollList
    -numberOfRows 5
    -allowMultiSelection true
    -width 150`;
}

global proc simulateFrames(){
    global string $startSimFrame;
    $startSimFrame = `intField
      -minValue 0
      -width 40
      -value 0`;
    global string $endSimFrame;
    $endSimFrame = `intField
      -minValue 1
      -width 40
      -value 100`;
}

global proc pfSimulateAction(){
  global string $startSimFrame;
  global string $endSimFrame;
  
  int $startFrame = `intField -query -v $startSimFrame`;
  int $endFrame = `intField -query -v $endSimFrame`;
  
  print $startFrame;
  print $endFrame;
  
  pfSimulate($startFrame, $endFrame);
}

$win = `window -title "fluid simulation" -widthHeight 175 300`;
  columnLayout;
    button -label "Load Fluid State..." -command "openFileBrowser";
    // button -label "Open OBJ sequence importer" -command "craOBJSequences";

    text "Bubbles:";
    appendBubbleList();
    button -label "+" -command "addBubbles";
    button -label "-" -command "removeBubbles";

    text "Simulate frames:";
    simulateFrames();
    
    button -label "Start simulation" -command "pfSimulateAction";
showWindow;
